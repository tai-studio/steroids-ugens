class:: TDemand
summary:: Demand results as trigger from demand rate UGens.
related:: Classes/Demand, Classes/Duty, Classes/TDuty
categories:: UGens>Demand

Description::

When there is a trigger at the trig input, a value is demanded each UGen
in the list and output. The unit generators in the list should be
'demand' rate.

When there is a trigger at the reset input, the demand rate UGens in the
list are reset.

classmethods::

method::ar, kr

argument::trig
Trigger. Can be any signal. A trigger happens when the signal
changes from non-positive to positive.

argument::reset
Trigger. Resets the list of UGens when triggered.

argument::demandUGens
List of demand-rate UGens to get values from. When the shortest stream ends, this ugen will set the link::Classes/Done##'done' flag::.

discussion::
By design, a reset trigger only resets the demand ugens; it does not reset the value at TDemand's output. TDemand continues to hold its value until the next value is demanded, at which point its output value will be the first expected item in the list.
To jump to the start value upon receipt of a reset trigger, one can add (+) the two triggers together:
code::
(
a = { |t_trig, t_reset|
	var	d = TDemand.kr(t_trig + t_reset, t_reset, Dseries(0, 1, inf));
	d.poll(t_trig + t_reset);
	0.0;
}.play
)
a.set(\t_trig, 1); // do this a few times -- the demand value should increase by 1 each time
a.set(\t_reset, 1);	// goes immediately back to 0
::

One demand ugen represents a single stream of values, so that embedding the same ugen twice calls this stream twice. To isolate a demand ugen, use a function:
code::
{
var a, b, t = Impulse.kr(2);
a = { Dseq([1, 2, 3, 4, 5], inf) } * 100;
b = a + 1;
TDemand.kr(t, 0, [a, b]).poll(t)
}.play
::


Examples::

code::
(
{
	var trig, seq, t_amp, t_scale;
	trig = Impulse.kr(12);
	t_amp = TDemand.kr(
		trig, 0, 
		Drand([Dseq((1..5).mirror1/5, 1), Drand((4..10), 8)/10], inf)
	);
	t_scale = TDemand.kr(
		trig, 0, 
		Drand([Dseq((1..5).mirror1/5, 1), Drand((4..10), 8)/10], inf) * 10
	);
		
	
	SinOscFB.ar(
		5000 + [0,0.7], 
		mul: Env.perc(0.001, 0.1).kr(gate: t_amp, levelScale: t_amp, timeScale: t_scale)
	).cubed.cubed ;
}.play;
)
::

